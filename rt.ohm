rt {
  Main = TopLevel+
  TopLevel =
    | Defvar -- defvar
    | Defn -- defn
    | Defclass -- defclass

   Defvar = kw<"defvar"> Assignment
   Defn = kw<"defn"> ident Formals? StatementBlock
   Defclass = kw<"defclass"> ident "{" Definit "}"

   StatementBlock = "{" Statement+ "}"

   Definit = kw<"definit"> "(" kw<"self"> ":" ident ")" "{" InitStatement+ "}"
   
   Statement =
     | kw<"useglobal"> "(" ident ")" -- global
     | IfStatement -- if
     | Assignment -- assignment
     | kw<"pass"> -- pass
     | kw<"return"> Exp -- return

   InitStatement = kw<"self"> "." ident "=" Exp

   IfStatement = kw<"if"> Exp StatementBlock ElifStatement* ElseStatement?
   ElifStatement = kw<"elif"> Exp StatementBlock
   ElseStatement = kw<"else"> StatementBlock

   Assignment = 
     | Lval "+=" Exp -- pluseq
     | Lval "=" Exp -- eq

    Exp
      = BooleanExp

    BooleanExp =
      | BooleanExp boolOp AddExp -- boolop
      | AddExp

    AddExp
      = AddExp "+" MulExp  -- plus
      | AddExp "-" MulExp  -- minus
      | MulExp

    MulExp
      = MulExp "*" ExpExp  -- times
      | MulExp "/" ExpExp  -- divide
      | ExpExp

    ExpExp
      = PriExp "^" ExpExp  -- power
      | PriExp

    PriExp =
      | "(" Exp ")"  -- paren
      | "[" "]" -- emptylistconst
      | "[" (PriExp ","?)+ "]" -- listconst
      | "{" "}" -- emptydict
      | "{" (Pair ","?)+ "}" -- dict
      | "λ" Formals? ":" Exp -- lambda
      | kw<"fresh"> "(" ident ")" -- fresh
      | PriExp Actuals -- call
      | "+" PriExp   -- pos
      | "-" PriExp   -- neg
      | phi -- phi
      | string -- string
      | ident FieldRef -- fieldref
      | ident Subscript -- subscript
      | ident -- ident
      | number -- number

    Lval = ident FieldRef?

    keyword =
      | kw<"fresh">
      | kw<"defvar">
      | kw<"defn">
      | kw<"defclass">
      | kw<"definit">
      | kw<"self">
      | kw<"useglobal">
      | kw<"pass">
      | kw<"return">
      | kw<"if">
      | kw<"elif">
      | kw<"else">
      | kw<"and">
      | kw<"or">
      
    ident  (an identifier)
      = ~keyword identHead identTail*

    identHead = "_" | letter
    identTail = alnum | identHead

    Formals = "(" ident ("," ident)* ")"
    Actuals = 
      | "(" ")" -- noactuals
      | "(" Exp ("," Exp)* ")" -- actuals

    number  (a number)
      = digit* "." digit+  -- fract
      | digit+             -- whole

    Pair = string ":" Exp ","?

  FieldRef = 
    | "[" string "]" FieldRef? -- field
    | "." ident Actuals FieldRef? -- method
    | "." ident FieldRef? -- attribute
    
  Subscript = 
    | "[" number "]"
    | "[" ident "]"

  boolOp = ("==" | "<=" | ">=" | ">" | "<" | kw<"and"> | kw<"or">) 
  phi = "ϕ"
  string =
    | "f" string -- fstring
    | basicstring -- basic
  basicstring =
    | "\"" notdq* "\"" -- dqstring
    | "'" notsq* "'" -- sqstring
  notdq = ~"\"" any
  notsq = ~"'" any

   kw<s> = s ~identTail

}